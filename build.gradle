buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'io.freefair.lombok' version '6.1.0'
    id 'io.ebean' version '12.13.0'
    id 'kr.entree.spigradle' version '2.3.2'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    depends = ['ebean-wrapper']
    softDepends = ['RCEconomy']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url = 'https://jitpack.io' }
    spigot()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly spigot(mcVersion)

    implementation 'de.raidcraft.economy:wrapper:2.4.0'
    implementation 'net.silthus:ebean-wrapper:2.9.1'
    implementation 'net.silthus.configlib:configlib-bukkit:2.6.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'

    implementation 'net.kyori:adventure-api:4.9.1'
    implementation 'net.kyori:adventure-text-minimessage:4.0.0-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'

    implementation group: 'io.art-framework', name: 'art-core', version: '4.5.4'

    testImplementation 'io.ebean:ebean-test:12.11.4'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.2'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.26'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.3'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.23'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.20.0'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('co.aikar::'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
        include(dependency('net.kyori::'))
        include(dependency('de.raidcraft.economy:wrapper:'))
    }
    relocate 'co.aikar.commands', "shadow.${pluginName}.acf"
    relocate 'co.aikar.locales', "shadow.${pluginName}.locales"
    relocate 'de.exlll.configlib', "shadow.${pluginName}.configlib"
    relocate 'net.kyori', "shadow.${pluginName}.text"
    relocate 'de.raidcraft.economy.wrapper', "shadow.${pluginName}.economy"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
